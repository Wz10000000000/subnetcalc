AC_INIT([SubNetCalc], [2.1.0~beta2], [dreib@iem.uni-due.de])

AM_INIT_AUTOMAKE
AC_PREREQ(2.60)

AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS([config.h:config.in])
AC_CONFIG_SRCDIR([src/subnetcalc.cc])

LT_INIT
AC_PROG_LIBTOOL
AC_PROG_CC
AC_PROG_CXX


# ###### Operating system checks ############################################
case $host_os in
freebsd*)
    ;;
linux*)
    ;;
openbsd*)
    ;;
solaris*)
    LDFLAGS="$LDFLAGS -lsocket -lnsl -lrt -lresolv"
    ;;
darwin*)
    ;;
esac


# ###### Networking declarations ############################################
AC_CHECK_HEADERS(sys/types.h sys/socket.h netinet/in.h)

AC_MSG_CHECKING(for socklen_t)
AC_TRY_COMPILE([#ifdef HAVE_SYS_TYPES_H
                #include <sys/types.h>
                #endif
                #include <sys/socket.h>],
               [socklen_t x = 1; return 0;],
               [AC_MSG_RESULT(yes)],
               [AC_MSG_RESULT(int)
                AC_DEFINE(socklen_t, int, [Define a type for socklen_t.])])

AC_MSG_CHECKING(if stderr is a variable of type FILE* and not a macro)
AC_TRY_COMPILE([#include <stdio.h>],
               [FILE** stdlog = &stderr],
               [AC_DEFINE(HAVE_STDERR_FILEPTR, 1, "Define to 1 if stderr is a pointer.")
                AC_MSG_RESULT(yes)],
               [AC_MSG_RESULT(no)])

AC_CHECK_MEMBER(struct sockaddr_in.sin_len,
                AC_DEFINE(HAVE_SIN_LEN, 1, [Define this if your IPv4 has sin_len in sockaddr_in struct.]),,
                [#ifdef HAVE_SYS_TYPES_H
                 #include <sys/types.h>
                 #endif
                 #include <netinet/in.h>])

AC_CHECK_MEMBER(struct sockaddr_in6.sin6_len,
                AC_DEFINE(HAVE_SIN6_LEN, 1, [Define this if your IPv6 has sin6_len in sockaddr_in6 struct.]),,
                [#ifdef HAVE_SYS_TYPES_H
                 #include <sys/types.h>
                 #endif
                 #include <netinet/in.h>])


# ###### GeoIP ##############################################################
GEOIP_LIBS=''
AC_MSG_CHECKING(whether to use the GeoIP IP address mapping library if available)

AC_ARG_WITH(geoip,
  AC_HELP_STRING( [--with-geoip@<:@=DIR@:>@],
                  [use GeoIP (located in directory DIR, if supplied).   @<:@default=no, if present@:>@]),
                  [
                     if   test "x$withval" = "xno";  then
                        want_geoip=no
                     elif test "x$withval" = "xyes"; then
                        want_geoip=yes
                     elif test -d "$withval"; then
                        want_geoip=yes
                        LDFLAGS="$LDFLAGS -L${withval}/lib"
                     fi
                  ])
if test "x$want_geoip" = "xno"; then
   AC_MSG_RESULT(no)
else
   AC_MSG_RESULT(yes)
   AC_CHECK_LIB(GeoIP, GeoIP_new,
                [ GEOIP_LIBS=-lGeoIP
                  AC_DEFINE(HAVE_GEOIP, 1, [Define to use GeoIP library]) ],
                [ AC_ERROR([GeoIP not found!]) ],)
fi
AC_SUBST(GEOIP_LIBS)


# ###### Makefiles ##########################################################
AC_CONFIG_FILES([Makefile src/Makefile])
AC_OUTPUT

